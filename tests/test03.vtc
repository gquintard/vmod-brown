varnishtest "capture() and group()"

server s1 {
	rxreq
	expect req.http.capture_all == /first/second/third
	expect req.http.capture_first == first
	expect req.http.capture_second == second
	expect req.http.capture_third == third
	txresp
} -start

varnish v1 -arg "-p thread_pool_stack=160k" -vcl+backend {
	import rers from "${vmod}";

	sub vcl_init {
		new re_store = rers.store(3);
	}

	sub vcl_recv {
		set req.http.premature = re_store.group(0);

		if (re_store.capture(req.url, "/([^/]*)/([^/]*)/([^/]*)")) {
			set req.http.capture_all = re_store.group(0);
			set req.http.capture_first = re_store.group(1);
			set req.http.capture_second = re_store.group(2);
			set req.http.capture_third = re_store.group(3);
		}
	}
} -start

client c1 {
	txreq -url "/first/second/third"
	rxresp
	expect resp.status == 200
} -run
